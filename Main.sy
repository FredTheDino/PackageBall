mod Main

enum State
    = Game GameState
    | Menu { fonts: Array Font }
type GameState =
  { shoe_spawn: Real
  , p : Player
  , things : Array Thing
  , score : Int
  , score_inc: Real
  , assets: Array Image
  , fonts: Array Font
  , box_p: ParticleSystem
  , score_p: ParticleSystem
  }
type Input = { left: Bool, right: Bool, up: Bool, down: Bool, hit: Bool, restart: Bool, exit: Bool }

def get_restart : Input -> Bool : { restart } = restart
def get_exit : Input -> Bool : { exit } = exit
def get_hit : Input -> Bool : { hit } = hit

def spy : a -> a := foreign -[[
    function(x)
        print(x)
        return x
    end
]]-

def init : Int -> State : _ =
  State:Menu { fonts: ~Asset.load_fonts }

def spawn_rate ::= 0.01

def update : Real -> Input -> State -> State : dt input state =
    match state
    with State:Game game_state -> update_game dt input game_state
    with State:Menu menu_state ->
        match get_hit input
        with true ->
            let assets = ~Asset.load_assets
            in
            State:Game
              { p: Player.new (Particle.dog_particle_system (Array.index_unsafe assets Asset.particle))
              , shoe_spawn: 0.0
              , score: 0
              , things: []
              , score_inc: 0.0
              , assets
              , fonts: ~Asset.load_fonts
              , box_p: Particle.box_particle_system (Array.index_unsafe assets Asset.particle)
              , score_p: Particle.score_particles (Array.index_unsafe assets Asset.particle)
              }
        with false ->
            State:Menu menu_state
        end
    end

def update_game : Real -> Input -> GameState -> State : dt input state@{ shoe_spawn, p, score, assets, things, score_inc, fonts, box_p, score_p } =
  let updated_things = things # Array.map (Thing.update dt p (get_hit input) things)
  let boxes_to_spawn = 1.0 + (score / 500)
  let should_spawn_box =
      things
        # Array.count_if (\t -> match t with Thing:Box {pos: _, vel} -> 0.1 < Vec.len_sq vel with _ -> false end)
        # (\x -> ^x < boxes_to_spawn)
  let c = Array.count_if Thing.is_box things
  let new_score = Array.range 0 (Array.size things - 1)
                # Array.foldl (\i acc ->
                    match { a: Array.index_unsafe things i, b: Array.index_unsafe updated_things i }
                    with { a: t@Thing:Box _, b: Maybe:Just (Thing:Full _) } ->
                      let _ = box_p # Particle.set_position (Thing.pos t) # Particle.emit 20
                      in acc + 100
                    with _ -> acc
                    end
                ) score
  in
  match { r: get_restart input, e: get_exit input } 
  with { r: true, e: _ } ->
    init 0 
  with { r: _, e: true } ->
    Love.quit 0 (State:Game state)
  with _ if ^c < (boxes_to_spawn + 8.0) ->
    State:Game
    { p: p # Player.update dt input # Player.box_collide things
    , things: updated_things
                # Array.filter_map (\x -> x)
                # Bool.when
                  should_spawn_box
                  (\_ -> Array.prepend $ Thing.spawn_box things)
                # Bool.when
                  (shoe_spawn < 0.0)
                  (\_ -> Array.prepend $ Thing.spawn_shoe things)
    , score: new_score
    , shoe_spawn:
          Bool.iff
            (shoe_spawn < 0.0)
            (3.0 * ~Math.random)
            (shoe_spawn - dt)
    , assets
    , score_inc: Bool.iff (score < new_score) (score_inc + 0.3) (Math.max (score_inc - dt) 0.0)
    , fonts
    , box_p: box_p # Particle.update dt
    , score_p: score_p # Particle.update dt
    }
  with _ ->
    State:Game { p, score, assets, things, shoe_spawn, score_inc, fonts, box_p, score_p }
  end

def draw
: State -> Render
: state =
    match state
    with State:Game game_state -> draw_game game_state
    with State:Menu { fonts } -> draw_menu fonts
    end

def draw_game
: GameState -> Render
: { p, score, score_inc, assets, things, fonts, box_p, score_p } =
  let pp = { x: 100.0, y: 200.0 }
  let r = 100.0
  in
  Render.empty
   *> Render.set_font (Array.index_unsafe fonts Asset.small_font)
   *> Render.color Render.white
   *> background assets
   *> Render.particle { x: 0.0, y: 0.0, r: 0.0, s: 1.0 } box_p
   *> Player.render assets p
   *> Render.all (Thing.draw assets) things
   *> show_score score_p score score_inc

def draw_menu : Array Font -> Render : fonts =
    let font = Array.index_unsafe fonts Asset.big_font
    let font_height = font # Font.height
    let boye_width = font # Font.width "Best Boye"
    let press_width = font # Font.width "Press Space to start"
    in
    Render.color Render.white
      *> Render.set_font font
      *> Render.printf "Best Boye" (~Render.width / 2.0 - boye_width / 2.0) (~Render.height / 2.0 - font_height) boye_width
      *> Render.printf "Press Space to start" (~Render.width / 2.0 - press_width / 2.0) (~Render.height / 2.0) press_width

def background : Array Image -> Render : assets =
  let img = Array.index_unsafe assets Asset.tile
  let size = Image.width img
  let needed_x = Real.floor (~Render.width / size)
  let needed_y = Real.floor (~Render.height / size)
  in
  Array.range 0 needed_x
    # Render.all (\x ->
      Array.range 0 needed_y
        # Render.all (\y ->
            Render.image { x: size * x + size / 2, y: size * y + size / 2, r: 0.0, s: 1.0 } img
        )
    )

def show_score
: ParticleSystem -> Int -> Real -> Render
: score_p score inc
=
  let rx = Math.sq (~Math.random) * inc * 30.0
  let ry = Math.sq (~Math.random) * inc * 30.0
  let _ = Bool.when (0.2 < inc) (\_ ps-> Particle.emit 1 ps) score_p
  in
  Render.particle { x: 110.0, y: `10.0, r: `Math.pi / 2.0, s: 1.0 } score_p
  *> Render.color Render.black
  *> Render.printf_
    ("Score: " ++ Int.to_string score)
    (Vec.vec (75.0 + rx) (50.0 + ry)) 
    "left"
    { r: 0.0, s: 1.0 + (inc * inc) * 2.0 }

